//@version=6

// @library Candlestick Pattern Library
// @description A comprehensive library for detecting single, two, and three-candle patterns.
//             Provides detailed pattern analysis including size, direction, and specific pattern types.
library("CandlePatternLibrary")

//+-------------------------------------------------------+
//                          ENUMS                         |
//+-------------------------------------------------------+

// @enum Size classification for candles
export enum CandleSize
    Short
    Normal
    Long

// @enum Direction classification for candles
export enum CandleDirection
    Bearish
    Neutral
    Bullish

// @enum Single candle patterns
export enum CandlePattern
    Unknown
    RegularBullish
    RegularBearish
    BullishMarubozu
    BearishMarubozu
    Hammer
    ShootingStar
    SpinningTop
    Doji
    LongLeggedDoji
    CrossDoji
    DragonflyDoji
    InvertedCrossDoji
    GravestoneDoji
    FourPriceDoji

// @enum Two candle patterns
export enum TwoCandlePattern
    None
    BullishEngulfingWeak
    BullishEngulfingStrong
    BearishEngulfingWeak
    BearishEngulfingStrong
    InsideBar
    TweezerTop
    TweezerBottom
    BullishRailRoad
    BearishRailRoad

// @enum Three candle patterns
export enum ThreeCandlePattern
    None
    ThreeWhiteSoldiers
    ThreeBlackCrows
    ThreeWhiteSoldiersWithBullishFVG
    ThreeWhiteSoldiersWithBearishFVG
    ThreeBlackCrowsWithBullishFVG
    ThreeBlackCrowsWithBearishFVG
    MorningStar
    EveningStar
    BullishAbandonedBaby
    BearishAbandonedBaby
    EngulfingSandwich
    BullishFairValueGap
    BearishFairValueGap

//+-------------------------------------------------------+
//                          TYPES                         |
//+-------------------------------------------------------+

// @type Complete analysis results for a single candle
// @field wickRange   Total range from high to low
// @field bodyRange   Range from open to close (absolute value)
// @field bodyHigh    Higher of open/close prices
// @field bodyLow     Lower of open/close prices
// @field size        Size classification (Short/Normal/Long)
// @field direction   Price direction (Bearish/Neutral/Bullish)
// @field pattern     Identified candlestick pattern
// @field openPrice   Original open price
// @field highPrice   Original high price
// @field lowPrice    Original low price
// @field closePrice  Original close price
export type CandleData
    float           wickRange
    float           bodyRange
    float           bodyHigh
    float           bodyLow
    CandleSize      size
    CandleDirection direction
    CandlePattern   pattern
    float           openPrice
    float           highPrice
    float           lowPrice
    float           closePrice

// @type Analysis results for two-candle patterns
// @field pattern  Identified two-candle pattern
// @field candle1  Analysis data for the first (older) candle
// @field candle2  Analysis data for the second (newer) candle
export type TwoCandleData
    TwoCandlePattern pattern
    CandleData       candle1
    CandleData       candle2

// @type Analysis results for three-candle patterns
// @field pattern  Identified three-candle pattern
// @field candle1  Analysis data for the first (oldest) candle
// @field candle2  Analysis data for the second (middle) candle
// @field candle3  Analysis data for the third (newest) candle
export type ThreeCandleData
    ThreeCandlePattern pattern
    CandleData         candle1
    CandleData         candle2
    CandleData         candle3

//+-------------------------------------------------------+
//                    HELPER FUNCTIONS                    |
//+-------------------------------------------------------+

// @function Test if two prices are equivalent within tolerance
// @param _price1 First price
// @param _price2 Second price
// @param _tolerance Absolute tolerance value
// @returns True if prices are within tolerance
isEquivalent(float _price1, float _price2, float _tolerance) =>
    math.abs(_price1 - _price2) <= _tolerance

// @function Determine candle size classification
// @param _wickRange Total wick range (high - low)
// @param _avgSize Average candle size for comparison
// @param _thresholdPct Percentage threshold for Long/Short classification
// @returns CandleSize enum value
determineCandleSize(float _wickRange, float _avgSize, float _thresholdPct) =>
    float longThreshold = _avgSize * (1.0 + _thresholdPct / 100.0)
    float shortThreshold = _avgSize * (1.0 - _thresholdPct / 100.0)
    
    CandleSize result = CandleSize.Normal
    if _wickRange > longThreshold
        result := CandleSize.Long
    else if _wickRange < shortThreshold
        result := CandleSize.Short
    result

// @function Determine candle direction
// @param _open Open price
// @param _close Close price
// @param _tolerance Equivalence tolerance
// @returns CandleDirection enum value
determineCandleDirection(float _open, float _close, float _tolerance) =>
    CandleDirection result = CandleDirection.Neutral
    if _close > _open + _tolerance
        result := CandleDirection.Bullish
    else if _close < _open - _tolerance
        result := CandleDirection.Bearish
    result
// #region Pattern Detection Functions ********************

is4PriceDoji(bool _highEquivLow, bool _hasEquivBody) =>
    _highEquivLow and _hasEquivBody

isDragonflyDoji(bool _hasEquivBody, bool _bodyAtHigh) =>
    _hasEquivBody and _bodyAtHigh

isGravestoneDoji(bool _hasEquivBody, bool _bodyAtLow) =>
    _hasEquivBody and _bodyAtLow

isCrossDoji(bool _hasEquivBody, bool _isNormalOrLong, bool _bodyInUpperSection) =>
    _hasEquivBody and _isNormalOrLong and _bodyInUpperSection

isInvertedCrossDoji(bool _hasEquivBody, bool _isNormalOrLong, bool _bodyInLowerSection) =>
    _hasEquivBody and _isNormalOrLong and _bodyInLowerSection

isLongLeggedDoji(bool _hasEquivBody, bool _isNormalOrLong, bool _bodyInMiddle) =>
    _hasEquivBody and _isNormalOrLong and _bodyInMiddle

isRegularDoji(bool _hasEquivBody, CandleSize _size) =>
    _hasEquivBody and _size == CandleSize.Short

isBullishMarubozu(bool _openEquivLow, bool _closeEquivHigh) =>
    _openEquivLow and _closeEquivHigh

isBearishMarubozu(bool _openEquivHigh, bool _closeEquivLow) =>
    _openEquivHigh and _closeEquivLow

isSmallBodyPattern(
  CandleSize _size,
  bool _hasSmallBody,
  bool _hasEquivBody
  ) =>
    (_size == CandleSize.Normal or _size == CandleSize.Long) and
      _hasSmallBody and not _hasEquivBody

getSmallBodyPatternType(bool _bodyInUpperSection, bool _bodyInLowerSection) =>
    CandlePattern result = CandlePattern.SpinningTop
    if _bodyInUpperSection
        result := CandlePattern.Hammer
    else if _bodyInLowerSection
        result := CandlePattern.ShootingStar
    result

// #endregion Pattern Detection Functions

// @function analyzeCandle                         - Analyzes a candle and returns comprehensive pattern data.
// @param    _open             (float)                 - Candle open price.
// @param    _high             (float)                 - Candle high price.
// @param    _low              (float)                 - Candle low price.
// @param    _close            (float)                 - Candle close price.
// @param    _avgSize          (float)                 - Average candle size for comparison.
// @param    _sizeThresholdPct (float)                 - Percentage threshold for size classification.
// @param    _equivTolerance   (float)                 - Absolute equivalence tolerance for Marubozu/Doji.
// @param    _bodyTolerance    (float)                 - Absolute body tolerance for small body detection.
// @param    _positionThreshold(int)                   - Position threshold (75-99) for hammer/shooting star.
// @returns                    (CandleData)            - Complete candle analysis data.
export analyzeCandle(
  float _open,
  float _high,
  float _low,
  float _close,
  float _avgSize,
  float _sizeThresholdPct,
  float _equivTolerance,
  float _bodyTolerance,
  int _positionThreshold
  ) =>
    // Calculate basic ranges
    float wickRange = _high - _low
    float bodyHigh = math.max(_open, _close)
    float bodyLow = math.min(_open, _close)
    float bodyRange = bodyHigh - bodyLow
    
    // Handle edge case: zero wick range
    float safeWickRange = wickRange > 0 ? wickRange : _avgSize * 0.001
    
    // Determine candle size classification
    CandleSize candleSize = determineCandleSize(wickRange, _avgSize, _sizeThresholdPct)
    
    // Determine candle direction using equivalence tolerance
    CandleDirection candleDir = determineCandleDirection(_open, _close, _equivTolerance)
    
    // Initialize pattern as Unknown
    CandlePattern pattern = CandlePattern.Unknown
    
    // Test equivalences for Marubozu detection (using equivalence tolerance)
    bool openEquivLow = isEquivalent(_open, _low, _equivTolerance)
    bool openEquivHigh = isEquivalent(_open, _high, _equivTolerance)
    bool closeEquivLow = isEquivalent(_close, _low, _equivTolerance)
    bool closeEquivHigh = isEquivalent(_close, _high, _equivTolerance)
    bool highEquivLow = isEquivalent(_high, _low, _equivTolerance)
    
    // Test for small body (using body tolerance)
    bool hasSmallBody = isEquivalent(_open, _close, _bodyTolerance)
    
    // Test for equivalent body (using stricter equivalence tolerance) - for Doji patterns
    bool hasEquivBody = isEquivalent(_open, _close, _equivTolerance)
    
    // Body position tests for equivalent body (Doji variations)
    bool bodyAtHigh = isEquivalent(bodyLow, _high, _equivTolerance) and isEquivalent(bodyHigh, _high, _equivTolerance)
    bool bodyAtLow = isEquivalent(bodyLow, _low, _equivTolerance) and isEquivalent(bodyHigh, _low, _equivTolerance)
    
    // Calculate position thresholds for body placement
    float upperThreshold = _low + safeWickRange * (_positionThreshold / 100.0)
    float lowerThreshold = _low + safeWickRange * ((100.0 - _positionThreshold) / 100.0)
    
    // Body position checks
    bool bodyInUpperSection = bodyLow >= upperThreshold
    bool bodyInLowerSection = bodyHigh <= lowerThreshold
    bool bodyInMiddle = not bodyInUpperSection and not bodyInLowerSection
    
    // Doji pattern conditions (using equivalence tolerance for body)
    bool isNormalOrLong = candleSize == CandleSize.Normal or candleSize == CandleSize.Long
    
    // Pattern determination logic (priority order)
    // Using focused helper functions for clarity and maintainability
    if is4PriceDoji(highEquivLow, hasEquivBody)
        pattern := CandlePattern.FourPriceDoji
    else if isDragonflyDoji(hasEquivBody, bodyAtHigh)
        pattern := CandlePattern.DragonflyDoji
    else if isGravestoneDoji(hasEquivBody, bodyAtLow)
        pattern := CandlePattern.GravestoneDoji
    else if isCrossDoji(hasEquivBody, isNormalOrLong, bodyInUpperSection)
        pattern := CandlePattern.CrossDoji
    else if isInvertedCrossDoji(hasEquivBody, isNormalOrLong, bodyInLowerSection)
        pattern := CandlePattern.InvertedCrossDoji
    else if isLongLeggedDoji(hasEquivBody, isNormalOrLong, bodyInMiddle)
        pattern := CandlePattern.LongLeggedDoji
    else if isRegularDoji(hasEquivBody, candleSize)
        pattern := CandlePattern.Doji
    else if isBullishMarubozu(openEquivLow, closeEquivHigh)
        pattern := CandlePattern.BullishMarubozu
    else if isBearishMarubozu(openEquivHigh, closeEquivLow)
        pattern := CandlePattern.BearishMarubozu
    else if isSmallBodyPattern(candleSize, hasSmallBody, hasEquivBody)
        pattern := getSmallBodyPatternType(bodyInUpperSection, bodyInLowerSection)
    else if candleDir == CandleDirection.Bullish
        pattern := CandlePattern.RegularBullish
    else if candleDir == CandleDirection.Bearish
        pattern := CandlePattern.RegularBearish
    else
        pattern := CandlePattern.Doji
    
    // Build and return result
    CandleData.new(
      wickRange,
      bodyRange,
      bodyHigh,
      bodyLow,
      candleSize,
      candleDir,
      pattern,
      _open,
      _high,
      _low,
      _close
      )

// @function isEngulfingPattern                    - Tests if two candles form an engulfing pattern.
// @param    _bodyEngulfs      (bool)                  - True if second candle body engulfs first candle body.
// @param    _c2Dir            (CandleDirection)       - Direction of the second candle.
// @param    _targetDir        (CandleDirection)       - Target direction (Bullish or Bearish).
// @returns                    (bool)                  - True if pattern matches engulfing criteria.
export isEngulfingPattern(bool _bodyEngulfs, CandleDirection _c2Dir, CandleDirection _targetDir) =>
    _bodyEngulfs and _c2Dir == _targetDir


// @function isInsideBarPattern                    - Tests if two candles form an inside bar pattern.
// @param    _c2High           (float)                 - High price of second candle.
// @param    _c1High           (float)                 - High price of first candle.
// @param    _c2Low            (float)                 - Low price of second candle.
// @param    _c1Low            (float)                 - Low price of first candle.
// @returns                    (bool)                  - True if second candle is inside first candle range.
export isInsideBarPattern(float _c2High, float _c1High, float _c2Low, float _c1Low) =>
    _c2High < _c1High and _c2Low > _c1Low


// @function isTweezerTopPattern                   - Tests if two candles form a tweezer top pattern.
// @param    _notShortCandles  (bool)                  - True if neither candle is short.
// @param    _equalHighs       (bool)                  - True if highs are equivalent.
// @param    _c1CloseEqualsC2Open (bool)               - True if first close equals second open.
// @param    _c1Dir            (CandleDirection)       - Direction of first candle.
// @param    _c2Dir            (CandleDirection)       - Direction of second candle.
// @param    _c1UpperWickPct   (float)                 - First candle upper wick percentage.
// @param    _c2UpperWickPct   (float)                 - Second candle upper wick percentage.
// @param    _minWickPct       (float)                 - Minimum wick percentage required.
// @returns                    (bool)                  - True if pattern matches tweezer top criteria.
export isTweezerTopPattern(
  bool _notShortCandles,
  bool _equalHighs,
  bool _c1CloseEqualsC2Open,
  CandleDirection _c1Dir,
  CandleDirection _c2Dir,
  float _c1UpperWickPct,
  float _c2UpperWickPct,
  float _minWickPct
  ) =>
    (
      _notShortCandles and
      _equalHighs and
      _c1CloseEqualsC2Open and
      _c1Dir == CandleDirection.Bullish and
      _c2Dir == CandleDirection.Bearish and
      _c1UpperWickPct >= _minWickPct and
      _c2UpperWickPct >= _minWickPct
      )


// @function isTweezerBottomPattern                - Tests if two candles form a tweezer bottom pattern.
// @param    _notShortCandles  (bool)                  - True if neither candle is short.
// @param    _equalLows        (bool)                  - True if lows are equivalent.
// @param    _c1CloseEqualsC2Open (bool)               - True if first close equals second open.
// @param    _c1Dir            (CandleDirection)       - Direction of first candle.
// @param    _c2Dir            (CandleDirection)       - Direction of second candle.
// @param    _c1LowerWickPct   (float)                 - First candle lower wick percentage.
// @param    _c2LowerWickPct   (float)                 - Second candle lower wick percentage.
// @param    _minWickPct       (float)                 - Minimum wick percentage required.
// @returns                    (bool)                  - True if pattern matches tweezer bottom criteria.
export isTweezerBottomPattern(
  bool _notShortCandles,
  bool _equalLows,
  bool _c1CloseEqualsC2Open,
  CandleDirection _c1Dir,
  CandleDirection _c2Dir,
  float _c1LowerWickPct,
  float _c2LowerWickPct,
  float _minWickPct
  ) =>
    (
      _notShortCandles and
      _equalLows and
      _c1CloseEqualsC2Open and
      _c1Dir == CandleDirection.Bearish and
      _c2Dir == CandleDirection.Bullish and
      _c1LowerWickPct >= _minWickPct and
      _c2LowerWickPct >= _minWickPct
      )


// @function isRailRoadPattern                     - Tests if two candles form a rail road pattern.
// @param    _c1Pattern        (CandlePattern)         - Pattern of first candle.
// @param    _c2Pattern        (CandlePattern)         - Pattern of second candle.
// @param    _c1Dir            (CandleDirection)       - Direction of first candle.
// @param    _c2Dir            (CandleDirection)       - Direction of second candle.
// @param    _targetDir        (CandleDirection)       - Target direction (Bullish or Bearish).
// @returns                    (bool)                  - True if pattern matches rail road criteria.
export isRailRoadPattern(
  CandlePattern _c1Pattern,
  CandlePattern _c2Pattern,
  CandleDirection _c1Dir,
  CandleDirection _c2Dir,
  CandleDirection _targetDir
  ) =>
    (
      // Both candles must be marubozu
      (_c1Pattern == CandlePattern.BullishMarubozu or _c1Pattern == CandlePattern.BearishMarubozu) and
      (_c2Pattern == CandlePattern.BullishMarubozu or _c2Pattern == CandlePattern.BearishMarubozu) and
      // Must be opposite directions
      _c1Dir != _c2Dir and
      // Second candle must match target direction
      _c2Dir == _targetDir
      )


// @function isThreeWhiteSoldiersPattern           - Tests if three candles form a Three White Soldiers pattern.
// @param    _c1Size           (CandleSize)             - Size of first candle.
// @param    _c2Size           (CandleSize)             - Size of second candle.
// @param    _c3Size           (CandleSize)             - Size of third candle.
// @param    _c1Dir            (CandleDirection)        - Direction of first candle.
// @param    _c2Dir            (CandleDirection)        - Direction of second candle.
// @param    _c3Dir            (CandleDirection)        - Direction of third candle.
// @returns                    (bool)                   - True if pattern matches Three White Soldiers criteria.
export isThreeWhiteSoldiersPattern(
  CandleSize _c1Size,
  CandleSize _c2Size,
  CandleSize _c3Size,
  CandleDirection _c1Dir,
  CandleDirection _c2Dir,
  CandleDirection _c3Dir
  ) =>
    (
      // All candles must be Normal or Long
      (_c1Size == CandleSize.Normal or _c1Size == CandleSize.Long) and
      (_c2Size == CandleSize.Normal or _c2Size == CandleSize.Long) and
      (_c3Size == CandleSize.Normal or _c3Size == CandleSize.Long) and
      // All must be bullish
      _c1Dir == CandleDirection.Bullish and
      _c2Dir == CandleDirection.Bullish and
      _c3Dir == CandleDirection.Bullish
      )


// @function isThreeBlackCrowsPattern              - Tests if three candles form a Three Black Crows pattern.
// @param    _c1Size           (CandleSize)             - Size of first candle.
// @param    _c2Size           (CandleSize)             - Size of second candle.
// @param    _c3Size           (CandleSize)             - Size of third candle.
// @param    _c1Dir            (CandleDirection)        - Direction of first candle.
// @param    _c2Dir            (CandleDirection)        - Direction of second candle.
// @param    _c3Dir            (CandleDirection)        - Direction of third candle.
// @returns                    (bool)                   - True if pattern matches Three Black Crows criteria.
export isThreeBlackCrowsPattern(
  CandleSize _c1Size,
  CandleSize _c2Size,
  CandleSize _c3Size,
  CandleDirection _c1Dir,
  CandleDirection _c2Dir,
  CandleDirection _c3Dir
  ) =>
    (
      // All candles must be Normal or Long
      (_c1Size == CandleSize.Normal or _c1Size == CandleSize.Long) and
      (_c2Size == CandleSize.Normal or _c2Size == CandleSize.Long) and
      (_c3Size == CandleSize.Normal or _c3Size == CandleSize.Long) and
      // All must be bearish
      _c1Dir == CandleDirection.Bearish and
      _c2Dir == CandleDirection.Bearish and
      _c3Dir == CandleDirection.Bearish
      )


// @function isMorningStarPattern                  - Tests if three candles form a Morning Star pattern.
// @param    _c1Size           (CandleSize)             - Size of first candle.
// @param    _c1Dir            (CandleDirection)        - Direction of first candle.
// @param    _c2Pattern        (CandlePattern)          - Pattern of middle candle.
// @param    _c3Size           (CandleSize)             - Size of third candle.
// @param    _c3Dir            (CandleDirection)        - Direction of third candle.
// @returns                    (bool)                   - True if pattern matches Morning Star criteria.
export isMorningStarPattern(
  CandleSize _c1Size,
  CandleDirection _c1Dir,
  CandlePattern _c2Pattern,
  CandleSize _c3Size,
  CandleDirection _c3Dir
  ) =>
    (
      // First candle must be bearish and Normal or Long
      (_c1Size == CandleSize.Normal or _c1Size == CandleSize.Long) and
      _c1Dir == CandleDirection.Bearish and
      // Middle candle must be a Doji (any type)
      (
        _c2Pattern == CandlePattern.Doji or
        _c2Pattern == CandlePattern.LongLeggedDoji or
        _c2Pattern == CandlePattern.CrossDoji or
        _c2Pattern == CandlePattern.DragonflyDoji or
        _c2Pattern == CandlePattern.InvertedCrossDoji or
        _c2Pattern == CandlePattern.GravestoneDoji or
        _c2Pattern == CandlePattern.FourPriceDoji
        ) and
      // Third candle must be bullish and Normal or Long
      (_c3Size == CandleSize.Normal or _c3Size == CandleSize.Long) and
      _c3Dir == CandleDirection.Bullish
      )


// @function isEveningStarPattern                  - Tests if three candles form an Evening Star pattern.
// @param    _c1Size           (CandleSize)             - Size of first candle.
// @param    _c1Dir            (CandleDirection)        - Direction of first candle.
// @param    _c2Pattern        (CandlePattern)          - Pattern of middle candle.
// @param    _c3Size           (CandleSize)             - Size of third candle.
// @param    _c3Dir            (CandleDirection)        - Direction of third candle.
// @returns                    (bool)                   - True if pattern matches Evening Star criteria.
export isEveningStarPattern(
  CandleSize _c1Size,
  CandleDirection _c1Dir,
  CandlePattern _c2Pattern,
  CandleSize _c3Size,
  CandleDirection _c3Dir
  ) =>
    (
      // First candle must be bullish and Normal or Long
      (_c1Size == CandleSize.Normal or _c1Size == CandleSize.Long) and
      _c1Dir == CandleDirection.Bullish and
      // Middle candle must be a Doji (any type)
      (
        _c2Pattern == CandlePattern.Doji or
        _c2Pattern == CandlePattern.LongLeggedDoji or
        _c2Pattern == CandlePattern.CrossDoji or
        _c2Pattern == CandlePattern.DragonflyDoji or
        _c2Pattern == CandlePattern.InvertedCrossDoji or
        _c2Pattern == CandlePattern.GravestoneDoji or
        _c2Pattern == CandlePattern.FourPriceDoji
        ) and
      // Third candle must be bearish and Normal or Long
      (_c3Size == CandleSize.Normal or _c3Size == CandleSize.Long) and
      _c3Dir == CandleDirection.Bearish
      )


// @function isBullishAbandonedBabyPattern         - Tests if three candles form a Bullish Abandoned Baby pattern.
// @param    _c1Size           (CandleSize)             - Size of first candle.
// @param    _c1Dir            (CandleDirection)        - Direction of first candle.
// @param    _c1Low            (float)                  - Low price of first candle.
// @param    _c2Pattern        (CandlePattern)          - Pattern of middle candle.
// @param    _c2High           (float)                  - High price of middle candle (Doji).
// @param    _c3Size           (CandleSize)             - Size of third candle.
// @param    _c3Dir            (CandleDirection)        - Direction of third candle.
// @param    _c3Low            (float)                  - Low price of third candle.
// @returns                    (bool)                   - True if pattern matches Bullish Abandoned Baby criteria.
export isBullishAbandonedBabyPattern(
  CandleSize _c1Size,
  CandleDirection _c1Dir,
  float _c1Low,
  CandlePattern _c2Pattern,
  float _c2High,
  CandleSize _c3Size,
  CandleDirection _c3Dir,
  float _c3Low
  ) =>
    (
      // First candle must be bearish and Normal or Long
      (_c1Size == CandleSize.Normal or _c1Size == CandleSize.Long) and
      _c1Dir == CandleDirection.Bearish and
      // Middle candle must be a Doji (any type)
      (
        _c2Pattern == CandlePattern.Doji or
        _c2Pattern == CandlePattern.LongLeggedDoji or
        _c2Pattern == CandlePattern.CrossDoji or
        _c2Pattern == CandlePattern.DragonflyDoji or
        _c2Pattern == CandlePattern.InvertedCrossDoji or
        _c2Pattern == CandlePattern.GravestoneDoji or
        _c2Pattern == CandlePattern.FourPriceDoji
        ) and
      // Doji high must be lower than both candle 1 and candle 3 lows (gap condition)
      _c2High < _c1Low and
      _c2High < _c3Low and
      // Third candle must be bullish and Normal or Long
      (_c3Size == CandleSize.Normal or _c3Size == CandleSize.Long) and
      _c3Dir == CandleDirection.Bullish
      )


// @function isBearishAbandonedBabyPattern         - Tests if three candles form a Bearish Abandoned Baby pattern.
// @param    _c1Size           (CandleSize)             - Size of first candle.
// @param    _c1Dir            (CandleDirection)        - Direction of first candle.
// @param    _c1High           (float)                  - High price of first candle.
// @param    _c2Pattern        (CandlePattern)          - Pattern of middle candle.
// @param    _c2Low            (float)                  - Low price of middle candle (Doji).
// @param    _c3Size           (CandleSize)             - Size of third candle.
// @param    _c3Dir            (CandleDirection)        - Direction of third candle.
// @param    _c3High           (float)                  - High price of third candle.
// @returns                    (bool)                   - True if pattern matches Bearish Abandoned Baby criteria.
export isBearishAbandonedBabyPattern(
  CandleSize _c1Size,
  CandleDirection _c1Dir,
  float _c1High,
  CandlePattern _c2Pattern,
  float _c2Low,
  CandleSize _c3Size,
  CandleDirection _c3Dir,
  float _c3High
  ) =>
    (
      // First candle must be bullish and Normal or Long
      (_c1Size == CandleSize.Normal or _c1Size == CandleSize.Long) and
      _c1Dir == CandleDirection.Bullish and
      // Middle candle must be a Doji (any type)
      (
        _c2Pattern == CandlePattern.Doji or
        _c2Pattern == CandlePattern.LongLeggedDoji or
        _c2Pattern == CandlePattern.CrossDoji or
        _c2Pattern == CandlePattern.DragonflyDoji or
        _c2Pattern == CandlePattern.InvertedCrossDoji or
        _c2Pattern == CandlePattern.GravestoneDoji or
        _c2Pattern == CandlePattern.FourPriceDoji
        ) and
      // Doji low must be higher than both candle 1 and candle 3 highs (gap condition)
      _c2Low > _c1High and
      _c2Low > _c3High and
      // Third candle must be bearish and Normal or Long
      (_c3Size == CandleSize.Normal or _c3Size == CandleSize.Long) and
      _c3Dir == CandleDirection.Bearish
      )


// @function isEngulfingSandwichPattern            - Tests if three candles form an Engulfing Sandwich pattern.
// @param    _c1High           (float)                  - High price of first candle.
// @param    _c1Low            (float)                  - Low price of first candle.
// @param    _c2High           (float)                  - High price of second candle.
// @param    _c2Low            (float)                  - Low price of second candle.
// @param    _c3High           (float)                  - High price of third candle.
// @param    _c3Low            (float)                  - Low price of third candle.
// @returns                    (bool)                   - True if pattern matches Engulfing Sandwich criteria.
export isEngulfingSandwichPattern(
  float _c1High,
  float _c1Low,
  float _c2High,
  float _c2Low,
  float _c3High,
  float _c3Low
  ) =>
    (
      // Candle 2 engulfs Candle 1
      _c2High > _c1High and _c2Low < _c1Low and
      // Candle 3 is inside Candle 2
      _c3High < _c2High and _c3Low > _c2Low
      )


// @function isBullishFairValueGapPattern          - Tests if three candles form a Bullish Fair Value Gap.
// @param    _c1High           (float)                  - High price of first candle.
// @param    _c3Low            (float)                  - Low price of third candle.
// @param    _minGap           (float)                  - Minimum absolute gap required.
// @returns                    (bool)                   - True if there is a gap at least _minGap wide.
export isBullishFairValueGapPattern(float _c1High, float _c3Low, float _minGap) =>
    _c3Low - _c1High >= _minGap


// @function isBearishFairValueGapPattern          - Tests if three candles form a Bearish Fair Value Gap.
// @param    _c1Low            (float)                  - Low price of first candle.
// @param    _c3High           (float)                  - High price of third candle.
// @param    _minGap           (float)                  - Minimum absolute gap required.
// @returns                    (bool)                   - True if there is a gap at least _minGap wide.
export isBearishFairValueGapPattern(float _c1Low, float _c3High, float _minGap) =>
    _c1Low - _c3High >= _minGap




// @function analyzeTwoCandlePattern               - Analyzes two consecutive candles for two-candle patterns.
// @param    _candle1          (CandleData)            - The first (older) candle data.
// @param    _candle2          (CandleData)            - The second (newer) candle data.
// @param    _equivTolerance   (float)                 - Absolute equivalence tolerance.
// @param    _positionThreshold(int)                   - Position threshold for wick length validation.
// @returns                    (TwoCandleData)         - Complete two-candle pattern analysis.
export analyzeTwoCandlePattern(
  CandleData _candle1,
  CandleData _candle2,
  float _equivTolerance,
  int _positionThreshold
  ) =>
    TwoCandlePattern pattern = TwoCandlePattern.None
    
    // Extract values for easier reference
    float c1BodyHigh = _candle1.bodyHigh
    float c1BodyLow = _candle1.bodyLow
    float c1High = _candle1.highPrice
    float c1Low = _candle1.lowPrice
    float c1Close = _candle1.closePrice
    
    float c2BodyHigh = _candle2.bodyHigh
    float c2BodyLow = _candle2.bodyLow
    float c2High = _candle2.highPrice
    float c2Low = _candle2.lowPrice
    float c2Open = _candle2.openPrice
    float c2Close = _candle2.closePrice
    
    CandleDirection c1Dir = _candle1.direction
    CandleDirection c2Dir = _candle2.direction
    CandleSize c1Size = _candle1.size
    CandleSize c2Size = _candle2.size
    
    // Calculate common values for pattern detection
    bool bodyEngulfs = c2BodyHigh > c1BodyHigh and c2BodyLow < c1BodyLow
    bool closedInsideRange = c2Close <= c1High and c2Close >= c1Low
    bool notShortCandles = c1Size != CandleSize.Short and c2Size != CandleSize.Short
    
    // Calculate equivalence checks
    bool equalHighs = isEquivalent(c1High, c2High, _equivTolerance)
    bool equalLows = isEquivalent(c1Low, c2Low, _equivTolerance)
    bool c1CloseEqualsC2Open = isEquivalent(c1Close, c2Open, _equivTolerance)
    
    // Calculate minimum wick percentage from position threshold
    // E.g., 85:15 means upper/lower wicks must be at least 15%
    float minWickPct = (100.0 - _positionThreshold) / 100.0
    
    // Calculate wick ranges and percentages
    float c1WickRange = c1High - c1Low
    float c2WickRange = c2High - c2Low
    
    // Upper wick percentages (for tweezer tops)
    float c1UpperWick = c1High - c1BodyHigh
    float c2UpperWick = c2High - c2BodyHigh
    float c1UpperWickPct = c1WickRange > 0 ? c1UpperWick / c1WickRange : 0
    float c2UpperWickPct = c2WickRange > 0 ? c2UpperWick / c2WickRange : 0
    
    // Lower wick percentages (for tweezer bottoms)
    float c1LowerWick = c1BodyLow - c1Low
    float c2LowerWick = c2BodyLow - c2Low
    float c1LowerWickPct = c1WickRange > 0 ? c1LowerWick / c1WickRange : 0
    float c2LowerWickPct = c2WickRange > 0 ? c2LowerWick / c2WickRange : 0
    
    // Pattern detection using library functions
    bool isTweezerTop = isTweezerTopPattern(
      notShortCandles, equalHighs, c1CloseEqualsC2Open,
      c1Dir, c2Dir, c1UpperWickPct, c2UpperWickPct, minWickPct
      )
    
    bool isTweezerBottom = isTweezerBottomPattern(
      notShortCandles, equalLows, c1CloseEqualsC2Open,
      c1Dir, c2Dir, c1LowerWickPct, c2LowerWickPct, minWickPct
      )
    
    bool isBullishEngulfing = isEngulfingPattern(bodyEngulfs, c2Dir, CandleDirection.Bullish)
    bool isBearishEngulfing = isEngulfingPattern(bodyEngulfs, c2Dir, CandleDirection.Bearish)
    bool isInsideBar = isInsideBarPattern(c2High, c1High, c2Low, c1Low)
    
    // Rail Road pattern detection (two opposite-direction marubozu candles)
    CandlePattern c1Pattern = _candle1.pattern
    CandlePattern c2Pattern = _candle2.pattern
    bool isBullishRailRoad = isRailRoadPattern(c1Pattern, c2Pattern, c1Dir, c2Dir, CandleDirection.Bullish)
    bool isBearishRailRoad = isRailRoadPattern(c1Pattern, c2Pattern, c1Dir, c2Dir, CandleDirection.Bearish)
    
    // Pattern determination (priority order)
    if isTweezerTop
        pattern := TwoCandlePattern.TweezerTop
    else if isTweezerBottom
        pattern := TwoCandlePattern.TweezerBottom
    else if isBullishRailRoad
        pattern := TwoCandlePattern.BullishRailRoad
    else if isBearishRailRoad
        pattern := TwoCandlePattern.BearishRailRoad
    else if isBullishEngulfing
        if closedInsideRange
            pattern := TwoCandlePattern.BullishEngulfingWeak
        else
            pattern := TwoCandlePattern.BullishEngulfingStrong
    else if isBearishEngulfing
        if closedInsideRange
            pattern := TwoCandlePattern.BearishEngulfingWeak
        else
            pattern := TwoCandlePattern.BearishEngulfingStrong
    else if isInsideBar
        pattern := TwoCandlePattern.InsideBar
    
    // Build and return result
    TwoCandleData.new(pattern, _candle1, _candle2)


// @function analyzeThreeCandlePattern             - Analyzes three consecutive candles for three-candle patterns.
// @param    _candle1          (CandleData)            - The first (oldest) candle data.
// @param    _candle2          (CandleData)            - The second (middle) candle data.
// @param    _candle3          (CandleData)            - The third (newest) candle data.
// @param    _minGap           (float)                 - Minimum absolute gap size for Fair Value Gap checks.
// @returns                    (ThreeCandleData)       - Complete three-candle pattern analysis.
export analyzeThreeCandlePattern(
  CandleData _candle1,
  CandleData _candle2,
  CandleData _candle3,
  float _minGap
  ) =>
    ThreeCandlePattern pattern = ThreeCandlePattern.None
    
    // Extract values for easier reference
    CandleSize c1Size = _candle1.size
    CandleSize c2Size = _candle2.size
    CandleSize c3Size = _candle3.size
    
    CandleDirection c1Dir = _candle1.direction
    CandleDirection c2Dir = _candle2.direction
    CandleDirection c3Dir = _candle3.direction
    
    CandlePattern c1Pattern = _candle1.pattern
    CandlePattern c2Pattern = _candle2.pattern
    CandlePattern c3Pattern = _candle3.pattern
    
    float c1High = _candle1.highPrice
    float c1Low = _candle1.lowPrice
    float c2High = _candle2.highPrice
    float c2Low = _candle2.lowPrice
    float c3High = _candle3.highPrice
    float c3Low = _candle3.lowPrice
    
    // Pattern detection using library functions
    bool isThreeWhiteSoldiers = isThreeWhiteSoldiersPattern(c1Size, c2Size, c3Size, c1Dir, c2Dir, c3Dir)
    bool isThreeBlackCrows = isThreeBlackCrowsPattern(c1Size, c2Size, c3Size, c1Dir, c2Dir, c3Dir)
    bool isMorningStar = isMorningStarPattern(c1Size, c1Dir, c2Pattern, c3Size, c3Dir)
    bool isEveningStar = isEveningStarPattern(c1Size, c1Dir, c2Pattern, c3Size, c3Dir)
    bool isBullishAbandonedBaby = isBullishAbandonedBabyPattern(c1Size, c1Dir, c1Low, c2Pattern, c2High, c3Size, c3Dir, c3Low)
    bool isBearishAbandonedBaby = isBearishAbandonedBabyPattern(c1Size, c1Dir, c1High, c2Pattern, c2Low, c3Size, c3Dir, c3High)
    bool isEngulfingSandwich = isEngulfingSandwichPattern(c1High, c1Low, c2High, c2Low, c3High, c3Low)
    bool isBullishFVG = isBullishFairValueGapPattern(c1High, c3Low, _minGap)
    bool isBearishFVG = isBearishFairValueGapPattern(c1Low, c3High, _minGap)
    
    // Pattern determination (priority order)
    // Check for combined patterns first (Three White Soldiers/Black Crows with FVG)
    // Abandoned Baby patterns take priority over Morning/Evening Stars since they are stricter
    if isThreeWhiteSoldiers and isBullishFVG
        pattern := ThreeCandlePattern.ThreeWhiteSoldiersWithBullishFVG
    else if isThreeWhiteSoldiers and isBearishFVG
        pattern := ThreeCandlePattern.ThreeWhiteSoldiersWithBearishFVG
    else if isThreeBlackCrows and isBullishFVG
        pattern := ThreeCandlePattern.ThreeBlackCrowsWithBullishFVG
    else if isThreeBlackCrows and isBearishFVG
        pattern := ThreeCandlePattern.ThreeBlackCrowsWithBearishFVG
    else if isThreeWhiteSoldiers
        pattern := ThreeCandlePattern.ThreeWhiteSoldiers
    else if isThreeBlackCrows
        pattern := ThreeCandlePattern.ThreeBlackCrows
    else if isBullishAbandonedBaby
        pattern := ThreeCandlePattern.BullishAbandonedBaby
    else if isBearishAbandonedBaby
        pattern := ThreeCandlePattern.BearishAbandonedBaby
    else if isMorningStar
        pattern := ThreeCandlePattern.MorningStar
    else if isEveningStar
        pattern := ThreeCandlePattern.EveningStar
    else if isEngulfingSandwich
        pattern := ThreeCandlePattern.EngulfingSandwich
    else if isBullishFVG
        pattern := ThreeCandlePattern.BullishFairValueGap
    else if isBearishFVG
        pattern := ThreeCandlePattern.BearishFairValueGap
    
    // Build and return result
    ThreeCandleData.new(pattern, _candle1, _candle2, _candle3)


// @function getPatternName                        - Returns the string name of a candle pattern.
// @param    _pattern          (CandlePattern)         - The candle pattern enum value.
// @returns                    (string)                - Human-readable pattern name.
export getPatternName(CandlePattern _pattern) =>
    switch _pattern
        CandlePattern.Unknown         => "Unknown"
        CandlePattern.RegularBullish  => "Regular Bullish"
        CandlePattern.RegularBearish  => "Regular Bearish"
        CandlePattern.BullishMarubozu => "Bullish Marubozu"
        CandlePattern.BearishMarubozu => "Bearish Marubozu"
        CandlePattern.Hammer          => "Hammer"
        CandlePattern.ShootingStar    => "Shooting Star"
        CandlePattern.SpinningTop     => "Spinning Top"
        CandlePattern.Doji            => "Doji"
        CandlePattern.LongLeggedDoji  => "Long Legged Doji"
        CandlePattern.CrossDoji       => "Cross Doji"
        CandlePattern.DragonflyDoji   => "Dragonfly Doji"
        CandlePattern.InvertedCrossDoji => "Inverted Cross Doji"
        CandlePattern.GravestoneDoji  => "Gravestone Doji"
        CandlePattern.FourPriceDoji   => "4 Price Doji"
        => "Unknown"


// @function getTwoCandlePatternName               - Returns the string name of a two-candle pattern.
// @param    _pattern          (TwoCandlePattern)      - The two-candle pattern enum value.
// @returns                    (string)                - Human-readable pattern name.
export getTwoCandlePatternName(TwoCandlePattern _pattern) =>
    switch _pattern
        TwoCandlePattern.None                 => "None"
        TwoCandlePattern.BullishEngulfingWeak => "Bullish Engulfing (Weak)"
        TwoCandlePattern.BullishEngulfingStrong => "Bullish Engulfing (Strong)"
        TwoCandlePattern.BearishEngulfingWeak => "Bearish Engulfing (Weak)"
        TwoCandlePattern.BearishEngulfingStrong => "Bearish Engulfing (Strong)"
        TwoCandlePattern.InsideBar            => "Inside Bar"
        TwoCandlePattern.TweezerTop           => "Tweezer Top"
        TwoCandlePattern.TweezerBottom        => "Tweezer Bottom"
        TwoCandlePattern.BullishRailRoad      => "Bullish Rail Road"
        TwoCandlePattern.BearishRailRoad      => "Bearish Rail Road"
        => "None"


// @function getThreeCandlePatternName             - Returns the string name of a three-candle pattern.
// @param    _pattern          (ThreeCandlePattern)    - The three-candle pattern enum value.
// @returns                    (string)                - Human-readable pattern name.
export getThreeCandlePatternName(ThreeCandlePattern _pattern) =>
    switch _pattern
        ThreeCandlePattern.None               => "None"
        ThreeCandlePattern.ThreeWhiteSoldiers => "Three White Soldiers"
        ThreeCandlePattern.ThreeBlackCrows    => "Three Black Crows"
        ThreeCandlePattern.ThreeWhiteSoldiersWithBullishFVG => "Three White Soldiers + Bullish FVG"
        ThreeCandlePattern.ThreeWhiteSoldiersWithBearishFVG => "Three White Soldiers + Bearish FVG"
        ThreeCandlePattern.ThreeBlackCrowsWithBullishFVG => "Three Black Crows + Bullish FVG"
        ThreeCandlePattern.ThreeBlackCrowsWithBearishFVG => "Three Black Crows + Bearish FVG"
        ThreeCandlePattern.MorningStar        => "Morning Star"
        ThreeCandlePattern.EveningStar        => "Evening Star"
        ThreeCandlePattern.BullishAbandonedBaby => "Bullish Abandoned Baby"
        ThreeCandlePattern.BearishAbandonedBaby => "Bearish Abandoned Baby"
        ThreeCandlePattern.EngulfingSandwich  => "Engulfing Sandwich"
        ThreeCandlePattern.BullishFairValueGap => "Bullish Fair Value Gap"
        ThreeCandlePattern.BearishFairValueGap => "Bearish Fair Value Gap"
        => "None"


// @function getSizeName                           - Returns the string name of a candle size.
// @param    _size             (CandleSize)            - The candle size enum value.
// @returns                    (string)                - Human-readable size name.
export getSizeName(CandleSize _size) =>
    switch _size
        CandleSize.Short  => "Short"
        CandleSize.Normal => "Normal"
        CandleSize.Long   => "Long"
        => "Unknown"


// @function getDirectionName                      - Returns the string name of a candle direction.
// @param    _direction        (CandleDirection)       - The candle direction enum value.
// @returns                    (string)                - Human-readable direction name.
export getDirectionName(CandleDirection _direction) =>
    switch _direction
        CandleDirection.Bearish => "Bearish"
        CandleDirection.Neutral => "Neutral"
        CandleDirection.Bullish => "Bullish"
        => "Unknown"

// #endregion Candle Pattern Library
